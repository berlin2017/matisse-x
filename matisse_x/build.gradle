/*
 * Copyright 2017 Zhihu Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

def glide_version = '4.9.0'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "it.sephiroth.android.library.imagezoom:library:$imagezoom_version"

    //图片显示 matisse需要glide或者picasso
    implementation "com.github.bumptech.glide:glide:$glide_version"

}

def libraryGroupId = 'cn.lonsun.android'
def libraryArtifactId = 'matisse_x'
def libraryVersion = '1.0.1'


publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
        }
    }
}
// Configure artifactory (for publishing dev)
//
// Based on NebulaOJOPublishingPlugin (from nebula-bintray-plugin)
artifactory {
    contextUrl = 'http://192.168.1.210/artifactory/'

    publish {
        repository {
            repoKey = 'mob-release'

            if (project.hasProperty('artifactory_username')) {
                username = project.property('artifactory_username')
                password = project.property('artifactory_password')
            }
        }

        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}